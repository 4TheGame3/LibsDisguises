plugins {
    id 'java'
    id "maven"
}

println 'Compiling LibsDisguises via Gradle ver. ' + gradle.gradleVersion

sourceCompatibility = '1.7'
ext.spigotVersion = '1.9-R0.1-SNAPSHOT'

ext.disguisesVersion = '9.0.3'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()

    maven {
        name 'Spigot'
        url 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }

    maven {
        url 'https://oss.sonatype.org/content/groups/public/'
    }

    maven {
        name 'dmulloy2-repo'
        url 'http://repo.dmulloy2.net/content/groups/public/'
    }

    flatDir {
        dir 'libs'
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

jar {
    baseName "LibsDisguises"
}

processResources {
    expand projectVersion: disguisesVersion
}

artifacts {
    archives sourceJar
    archives javadocJar
    archives jar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "http://repo.md-5.net/content/groups/public/")
            pom.groupId = 'LibsDisguises'
            pom.version = disguisesVersion
            pom.artifactId = 'LibsDisguises'
        }
        mavenDeployer {
            repository(url: "http://repo.md-5.net/content/repositories/snapshots/")
            pom.groupId = 'LibsDisguises'
            pom.version = disguisesVersion + "-SNAPSHOT"
            pom.artifactId = 'LibsDisguises'
        }
    }
}

file('libs').mkdirs()

ant.get src: 'http://server.o2gaming.com/downloads/spigot-1.9.jar', dest: file('libs'), verbose: false, skipexisting: true

dependencies {
    compile "org.spigotmc:spigot-api:$project.ext.spigotVersion"
    compile 'com.comphenix.protocol:ProtocolLib:3.7.0-SNAPSHOT'
    compile group: "spigot", name: 'spigot-1.9', version: "1.9"
    testCompile group: 'junit', name: 'junit', version: '4.10'
}
