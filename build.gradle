plugins {
    id 'java'
    id 'maven-publish'
    id "com.jfrog.artifactory" version "3.1.1"
}

println 'Compiling LibsDisguises via Gradle ver. ' + gradle.gradleVersion

sourceCompatibility = '1.7'
ext.spigotVersion = '1.8.8-R0.1-SNAPSHOT'

ext.disguisesVersion = '8.6.5'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {

    mavenCentral()

    maven {
        name 'Spigot'
        url 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }

    maven {
        name 'Vault-Repo'
        url 'http://nexus.theyeticave.net/content/repositories/pub_releases'
    }

    maven {
        name 'dmulloy2-repo'
        url 'http://repo.dmulloy2.net/content/groups/public/'
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

jar {
    baseName "LibsDisguises"
}

artifacts {
    archives sourceJar
    archives javadocJar
    archives jar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'LibsDisguises'
            artifactId 'LibsDisguises'
            version disguisesVersion + '-SNAPSHOT'
            from components.java
            artifact sourceJar {
                classifier = 'sources'
            }
            artifact javadocJar {
                classifier = 'javadoc'
            }
        }
    }
}

artifactoryPublish.skip = false

artifactory {
    contextUrl = 'http://localhost:8081/artifactory'
    publish {
        repository {
            repoKey = 'libs-snapshot-local'
            username = '${artifactory_user}'
            password = '${artifactory_password}'
        }
        defaults {
            publications('mavenJava')
        }
    }
    resolve {
        repoKey = 'repo'
    }
}

file('libs').mkdirs()

//Retrieves spigot.jar server file from link
ant.get src: 'https://www.dropbox.com/s/1ofgubrldjwamnk/spigot.jar?dl=1', dest: file('libs'), verbose: false, skipexisting: true

dependencies {
    compile 'org.bukkit:bukkit:' + project.ext.spigotVersion
    compile 'com.comphenix.protocol:ProtocolLib:3.6.5-SNAPSHOT'
    compile files(
            'libs/spigot.jar'
    )
    testCompile group: 'junit', name: 'junit', version: '4.10'
}
